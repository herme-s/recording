#
#  Copyright (c) 2015 Stefan Parvu (www.kronometrix.org).
#
#  This program is free software; you can redistribute it and/or
#  modify it under the terms of the GNU General Public License
#  as published by the Free Software Foundation; either version 2
#  of the License, or (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program; if not, write to the Free Software Foundation,
#  Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
#
#  (http://www.gnu.org/copyleft/gpl.html)

package PerlSvc;

use strict;
use warnings;
use File::Tail;
use File::Temp qw(tempfile);
use File::Copy;
use File::Basename;
use UUID::Tiny ':std';
use Digest::SHA;
use Getopt::Long;
use Time::HiRes qw(time);
use Win32::OLE ('in');
use HTTP::Tiny;
use JSON;


###  Process command line args
usage() if defined $ARGV[0] and $ARGV[0] eq "--help";

# default timeout
my $timeout         = 25;
my $transid         = 'sender';
my $cprefix         = 'C:\\Program Files\\Kronometrix\\etc';
my ($h, $v, $V)     = 0;
our %Config         = (ServiceName => $transid);

Getopt::Long::Configure('bundling');
my $result = GetOptions (
                "t|timeout=i" => \$timeout,
                "v|verbose"   => \$v,
                "h|help"      => \$h,
                "V|version"   => \$V,
                "n|name=s"    => \$Config{ServiceName}
                );

usage() if ($h || ! $result);
revision() if (defined $V);

# process [interval]
my $interval;

if ( defined $ARGV[0] ) {
    $interval = $ARGV[0];
    usage() if $interval == 0;
}
else {
    $interval = 60;
}

### Variables
my $loop        = 0;                      # current loop number
my $cfile       = 'kronometrix.json';     # configuration file
my $cdata;                                # configuration data
my $tcpka       = 0;                      # transport TCP keepalive settings
my $sid         = "";                     # subscription id
my $token       = "NA";                   # token id
my @files;
my $hostid;
my ($baselog, $curlog, $slog);
my @dbs;
my %appliance;                            # appliance coordinates
$|= 1;                                    # autoflush

my $verbose = defined $v ? $v : 0;

### MAIN BODY #

# Default krd
my @recs = qw(sys cpu disk nic hdw);

# daemon name
my $dname = basename($0);

# osname
my $osname = "$^O";
$osname = "win" if $osname eq "MSWin32";

# read configuration
$cdata = open_config($cfile);

## hostid
get_hostid($cdata);

## logs
($baselog, $curlog) = getlog($cdata);
$slog = $baselog . "\\sender.log";

# open log file
my $log = open_file($slog);

## datafile
@dbs = get_datafile($cdata);

## appliance destination
%appliance = get_appliance($cdata);

writelog ("info: started");

# cd krd raw data directory
chdir("$curlog")
  or die "error: cannot open raw data directory, $!\n";

foreach (@dbs) {
    my $n = $_ . "rec.krd";
    push(
        @files,
        File::Tail->new(
            name               => "$n",
            interval           => 1,
            maxinterval        => $interval,
            ignore_nonexistant => 1
        )
    );
}

# init HTTP transport
my $http = HTTP::Tiny->new(keep_alive => 0,
                           agent => 'Kronometrix_Win/1.0');


unless (defined &ContinueRun) {
    *ContinueRun = sub { return 1 };
    *RunningAsService = sub { return 0 };
    Interactive();
}


if (!RunningAsService()) {
    if ($verbose) {
        print "appliance: " . keys(%appliance) . ".\n";
        for my $key ( keys %appliance ) {
            my $value = $appliance{$key};
            print "$key => $value\n";
        }
    }
}

### Subroutines

sub Startup {

    # main loop
    while (ContinueRun()) {

    my ( $nfound, $timeleft, @pending ) =
      File::Tail::select( undef, undef, undef, $timeout, @files );

    # timeout - do something else here, if you need to
    unless ($nfound) {
        writelog("info: no krd raw data found, checking...")
          if ( $verbose == 1 );
    }
    else {

        ## raw data arrival
        writelog("info: krd raw data found")
        if ( $verbose == 1 );

        my $rcode = 0;
        foreach (@pending) {
            my $delta = $_->read;
            chomp($delta);

            # parse id
            my $msgid;
            my $devid;
            my $lmoid;

            if ( $_->{"input"} =~ /sysrec/ ) {
                $lmoid = 'cpd';
                $devid = 'system';
                $msgid  = "$osname-sysrec";
            }
            elsif ( $_->{"input"} =~ /cpurec/ ) {
                $lmoid = 'cpd';
                $devid = get_devid( 1, $delta );
                $msgid = "$osname-cpurec";
            }
            elsif ( $_->{"input"} =~ /diskrec/ ) {
                $lmoid = 'cpd';
                $devid = get_devid( 1, $delta );
                $msgid = "$osname-diskrec";
            }
            elsif ( $_->{"input"} =~ /nicrec/ ) {
                $lmoid = 'cpd';
                $devid = get_devid( 1, $delta );
                $msgid = "$osname-nicrec";
            }
            elsif ( $_->{"input"} =~ /netrec/ ) {
                $lmoid = 'cpd';
                $devid = 'system';
                $msgid  = "$osname-netrec";
            }
            elsif ( $_->{"input"} =~ /jvmrec/ ) {
                $lmoid = 'cpd';
                $devid = 'system';
                $msgid  = "$osname-jvmrec";
            }
            elsif ( $_->{"input"} =~ /procrec/ ) {
                $lmoid = 'cpd';
                $devid = 'system';
                $msgid  = "$osname-procrec";
            }
            elsif ( $_->{"input"} =~ /zonerec/ ) {
                $lmoid = 'cpd';
                $devid = 'system';
                $msgid  = "$osname-zonerec";
            }
            elsif ( $_->{"input"} =~ /webrec/ ) {
                $lmoid = 'cpd';
                $devid = 'system';
                $msgid  = "$osname-webrec";
            }
            elsif ( $_->{"input"} =~ /hdwrec/ ) {
                $lmoid = 'cpd';
                $devid = 'system';
                $msgid  = "$osname-hdwrec";
           }
            elsif ( $_->{"input"} =~ /xenrec/ ) {
              $lmoid = 'cpd';
                $devid = 'system';
                $msgid  = "$osname-xenrec";
            }
            elsif ( $_->{"input"} =~ /wsrec/ ) {
                $lmoid = 'wcd';
                $devid = 'system';
                $msgid  = "$osname-wsrec";
            }

            # each appliance
            for my $key ( keys %appliance ) {
                my $value = $appliance{$key};

                ## value format
                # cpd:hostname2:443:00000000000000000000000000000000
                # lmo:host:port:sid => $token:$proto:$ka
                my ( $lid, $hname, $port, $sid ) = split ( /:/, $key );
                my ( $dsid, $tid, $proto, $ka ) = split( /:/, $value );

                # writelog("info: $lid => $hname:$port:$sid  value=$dsid:$tid:$proto:$ka"
                #) if ( $verbose == 1 );

                if ($lid eq $lmoid ) {
                    
                    # writelog("debug: before sending ... $lid/$msgid => $hname:$port:$sid:$tid");
                    send_data( $http, $hname, $sid, $tid, $dsid,
                        $msgid, $devid, $proto, $port, $delta );
                }
            }
            print "\n" . $_->{"input"} . " payload[" . $delta . "]"
              if ( $verbose == 1 );

        }    # for

    }    # else raw data

    }    # while loop


    if (RunningAsService()) {
        writelog ("info: stopped");
    }

}


sub Interactive {

    Install();

    if (( ! defined $h ) and (! defined $V)) {
        Startup();
    }
}


sub Install {

    if (RunningAsService()) {
        # check for proper config file
        if (! defined $cdata) {
            die "Cant install $Config{ServiceName} service, exiting...\n";
        }

        if (! defined $curlog ) {
            die "Cant install $Config{ServiceName} service, exiting...\n";
        }
    }

    $Config{DisplayName} = "Kronometrix $Config{ServiceName} Service";
    $Config{Description} = "Kronometrix $transid Service";

    return;
}


sub Remove {

    $Config{ServiceName} = $transid;
    return;
}


# open_config - open JSON configuration file
#
sub open_config {

    my ($conf) = @_;
    my $json_data;

    {
        local $/;

        # we will parse now the file
	if ( defined $ENV{'KRMX_PREFIX'} ) {
	    if ( -e "$ENV{'KRMX_PREFIX'}/etc/$conf" ) {
	        open my $fh, "<", "$ENV{'KRMX_PREFIX'}/etc/$conf";
	        $json_data = <$fh>;
	        close $fh;
	    } else {
                die "error: open_config - $! $ENV{'KRMX_PREFIX'}/etc/$conf \n";
	    }
	} else {
	    if ( -e "$cprefix\\$conf" ) {
		open my $fh, "<", "$cprefix\\$conf";
		$json_data = <$fh>;
		close $fh;
	    } else { 
	        die "error: open_config - $! $conf \n"; 
	    }
	} 
    }
    
    my $perl_data = JSON->new->utf8->decode($json_data); 
    
    return $perl_data;
}


# write config file
sub write_config {

    my ( $conf, $perl_data ) = @_;

    # JSON Object
    my $json_data = JSON->new->utf8->pretty->encode($perl_data);

    # we will parse now the file
    if ( defined $ENV{'KRMX_PREFIX'} ) {
        if ( -e "$ENV{'KRMX_PREFIX'}/etc/$conf" ) {
            open my $fh, ">", "$ENV{'KRMX_PREFIX'}/etc/$conf.tmp";

            # print $fh encode_json($data);
            print $fh $json_data;
            close $fh;
            move(
                "$ENV{'KRMX_PREFIX'}/etc/$conf.tmp",
                "$ENV{'KRMX_PREFIX'}/etc/$conf"
            ) or die "Error: cannot update $conf: $!";

        }
        else {
            print "error: write_config - $! $ENV{'KRMX_PREFIX'}/etc/$conf \n";
            usage();
        }
    }
    else {
	if ( -e "$cprefix\\$conf" ) {
	    open my $fh, ">", "$cprefix\\$conf.tmp";
            print $fh $json_data;
            close $fh;
            move( "$cprefix\\$conf.tmp", "$cprefix\\$conf" )
              or die "Error: cannot update $conf: $!";
        }
        else {
            die "error: write_config - $! $cprefix\\$conf \n";
        }
    }
}


# get_devid, returns device_id
sub get_devid {

    my ( $pos, $krd ) = @_;
    my $device_id;

    my @krd = split( /:/, $krd );

    $device_id = $krd[$pos];

    writelog("info: get_devid - devid=$device_id")
      if ( $verbose == 1 );

    # device_id
    return $device_id;
}


# get hostuuid
sub new_hostid {

    my $hid;

    # get stats
    my $wmi = Win32::OLE->GetObject("winmgmts://./root/cimv2")
      or die "Cannot initialize WMI interface\n";


    my $wcsp = $wmi->InstancesOf("Win32_ComputerSystemProduct");
    foreach my $obj (in $wcsp) {
        $hid = $obj->{UUID};
    }

    # fetches machine-id Win32
    if ( defined $hid ) {
        $hid = lc "$hid";
    }
    else {
        my $str = rand(time) . $osname;
        $hid = create_uuid_as_string(UUID_V5,$str);
    }

    chomp($hid);

    # return new host UUID;
    return $hid;
}


# get hostid
sub get_hostid {

    my ($data) = @_;

    # get hostuuid
    my $h = new_hostid;

    # populate DSID
    make_dsid($data, $h);
}


# make data source ids, dsid
sub make_dsid {

    my ($data, $huuid) = @_;
    my @lmo = ('cpd', 'amd', 'wcd');

    # sid - subscription id
    # dsid - data source id
    # get all sids and generate dsid for each
    # dsid must be unique per sid

    my @applx = @{ $data->{'transport'}->{'appliance'} };

    # populate dsid for each appliance and lmo
    foreach my $appliance (@applx) {
        my ($sid, $tid, $dsid);

        my $host  = $appliance->{'host'};
        my $port  = $appliance->{'port'};

        # last call, look for subscription id, sid

        foreach my $mo (@lmo) {

            if ($appliance->{"$mo"}) {

                my @sub = @{ $appliance->{"$mo"} };

                foreach my $sd (@sub) {

                    # get sid, tid
                    $sid = $sd ->{'sid'};
                    $tid = $sd ->{'tid'};

                    # get dsid
                    $dsid = $sd ->{'dsid'};
                   
                    if ($dsid eq "" ) {

                        if ( $sid ne "" ) {
                            my $str = $huuid . $sid;
                            $dsid = create_uuid_as_string(UUID_V5,$str);
                            chomp($dsid);
                            # print "sid=$sid tid=$tid dsid=$dsid\n";
                            $sd->{'dsid'} = $dsid;
                            write_config($cfile, $data);
                        } else {
                            exit 1;
                        }
                    } else {
                        my $str = $huuid . $sid;
                        my $dsid_man = create_uuid_as_string(UUID_V5,$str);
                        chomp($dsid_man);

                        if ( $dsid ne $dsid_man ) {
                            # we need to update json file
                            # print "Not OK: $dsid => $dsid_man\n";
                            $sd->{'dsid'} = $dsid_man;
                            write_config($cfile, $data);
                        }
                    }
                }
            }
        }
    }
}


# chklog - check log directory
#
sub chklog {

    my ($dir) = @_;

    if (defined $dir) {
        if (! -d $dir) {
            die "error: chklog - cannot find $dir directory";
        }
    } else {
        die "error: chklog - cannot find $dir directory";
    }

    return;
}


# open_data - open data file
#
sub open_file {

    my ($file) = @_;
    my $fh;

    if (-f $file) {
        open $fh, "+>>", "$file" or 
	  die "error: open_file - cannot open $file $!";
	seek $fh, 0, 2;
        select ((select ($fh), $| = 1)[0]);

    } else {
        open $fh, "+>", "$file" or 
	  die "error: open_file - cannot open $file $!";
        select ((select ($fh), $| = 1)[0]);

    }

    return $fh;
}


# getlog - get log directory information
#
sub getlog {
    my ($data) = @_;

    my $bpath = $data->{'log'}->{'base_path'};
    chklog($bpath);

    my $cpath = $data->{'log'}->{'current_path'};
    chklog($cpath);

    return ( $bpath, $cpath );
}


# get datafile
sub get_datafile {

    my ($data) = @_;
    my $schilds = 0;
    my @d;
    my @temp = @{ $data->{'transport'}->{'data_file'} };

    # load d array
    foreach my $f (@temp) {
        push @d, $f->{"name"};
        $schilds++;
    }

    @d = @recs if ( $schilds == 0 );

    return @d;
}


# get appliance information
sub get_appliance {

    my ($data) = @_;
    my %appl;

    my @lmo = ('cpd', 'amd', 'wcd');

    my @temp = @{ $data->{'transport'}->{'appliance'} };

    foreach my $f (@temp) {
        my ($sid, $tid, $dsid);

        my $ka    = $f->{'keepalive'};
        my $proto = $f->{'protocol'};
        my $host  = $f->{'host'};
        my $port  = $f->{'port'};

        # last call, look for subscription

        foreach my $mo (@lmo) {

            if ($f->{"$mo"}) {

                my @sub = @{ $f->{"$mo"} };

                foreach my $sd (@sub) {

                    # get sid, tid, dsid
                    $sid  = $sd ->{'sid'};
                    $tid  = $sd ->{'tid'};
                    $dsid = $sd ->{'dsid'};

                    if ( defined($host)
                         and defined($sid)
                         and defined($dsid)
                         and defined($tid)
                         and defined($proto)
                         and defined($port)
                         and defined($ka) ) {
                        my $keyapp = $mo . ":" . $host . ":" . $port . ":" . $sid;
                        $appl{$keyapp} = "$dsid:$tid:$proto:$ka";
                    }
                }
            }
        }
    }

    return %appl;
}


## auxiliares

# writelog - write log message
#
sub writelog {

    my ($logbuf) = @_;
    my ( $sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst ) =
    localtime(time);

    my $dt = sprintf "%4d-%02d-%02d %02d:%02d:%02d",
                $year + 1900, $mon + 1, $mday, $hour, $min, $sec;

    if (eof $log) {
        print $log "$dt $logbuf\n";
    } else {
        print $log "\n$dt $logbuf";
    }

    return;
}


sub send_data {

    my (
        $curl,  $dest,   $sid,   $token, $dsid,
        $msg_id, $dev_id, $proto, $port,  $delta
    ) = @_;

    my $response;

    ## http://server:port/api/private/send_data
    my $url = "$proto://" . $dest . ":" . $port . "/api/private/send_data";

    my ($hash, $post);

    if ( $msg_id =~ /cpurec|diskrec|nicrec/ ) {
        $hash   = sha256krd("$msg_id:$sid:$dsid:$delta");
        $post   = "$msg_id:$sid:$dsid:$delta:$hash";
    } else {
        $hash   = sha256krd("$msg_id:$sid:$dsid:$dev_id:$delta");
        $post   = "$msg_id:$sid:$dsid:$dev_id:$delta:$hash";
    }
    writelog("info: send_data - $post data to be delivered")
        if ( $verbose == 1 );


    # writelog("info: send_data - post=$post");

    $response = $http->request('POST', $url, {
        content => $post,
        headers => { 'Token' => "$token",
    	             'content-type' => 'application/x-www-form-urlencoded'},});

    my $status = $response->{status};

    if ( $verbose == 1 ) {

        if ($response->{success} ) {
            writelog("info: send_data - $msg_id krd data delivered");
        } else {
            writelog(
            "error: send_data - cant send $msg_id krd data, transport error");
        }
    }

    return;
}


# NIST SHA-256 message digest for krd raw data
sub sha256krd {
    my ($message) = @_;

    # compute SHA-256
    my $state = Digest::SHA->new(256);

    $state->add($message);
    my $digest = $state->hexdigest;

    return $digest;
}

# usage - print usage and exit.
sub usage {
    print STDERR <<END;   
USAGE: sender [-t secs] [-hvV] | [interval]
OPTIONS:
  -h | --help              : help information
  -v | --verbose           : verbose information
  -V | --version           : release version
  -t secs | --timeout secs : timeout in seconds, default 25s
  interval                 : max number of seconds between samples, default 60s 

 eg, 
  sender                # default mode, transport raw data every 60s
  sender 10             # transport raw data every 10s
  sender -t5 10         # transport raw data every 10s with 5s timeout
  sender -install auto  # run as a service, every 60s, 25s timeout
  sender -remove        # stop and remove the service
END
    return;
}

# revision - print revision and exit
sub revision {
    print STDERR <<END;
sender: 1.0.16, 2015-08-27 1655
END
    return;
}
